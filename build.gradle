/*
 * Copyright 2013-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.text.SimpleDateFormat

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'
  }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'maven-publish'
apply plugin: 'groovy'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()
  implementation 'org.apache.commons:commons-lang3:3.11'
  implementation 'nz.net.ultraq.lesscss:lesscss-compiler:1.0.2'
  implementation 'org.apache.velocity:velocity:1.7'
  implementation 'commons-io:commons-io:2.8.0'
  implementation 'org.eclipse.xtend:org.eclipse.xtend.lib:2.24.0'
  implementation 'net.lingala.zip4j:zip4j:1.3.3' // 2.6.4
  implementation 'com.sun.activation:javax.activation:1.2.0'

  testImplementation('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  testImplementation 'cglib:cglib-nodep:2.2.2'
}

task sourceJar(type: Jar) {
  group 'Build'
  description 'An archive of the source code'
  classifier 'sources'
  from sourceSets.main.allSource
}

test {
  exclude 'com/metrixware/**' //exclude the tests since there is some platform issues that doesn't impact final product
}

artifacts { sourceJar }


Date buildTimeAndDate = new Date()
ext {
    buildTime = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildDate = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}


jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.getProperty("java.vm.version") + ")",
            'Build-Date': project.buildTime,
            'Build-Time': project.buildDate,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'https://github.com/sleroy/gradle-pandoc-plugin',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'https://github.com/sleroy/gradle-pandoc-plugin'
        )
    }
}

def pomConfig = {
    name project.name
    description 'A Gradle plugin for transforming Markdown/Latex to HTML/PDF/EPUB/Eclipse with Pandoc'
    url 'https://github.com/sleroy/gradle-pandoc-plugin'
    inceptionYear '2014'
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/sleroy/gradle-pandoc-plugin'
    }
    developers {
        [
            sleroy: 'Sylvain Leroy',
            afloch : 'Antoine Floch'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourceJar

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

task install(dependsOn: publishToMavenLocal)
